plugins {
    id "com.android.application"
    id "kotlin-android"
    id 'kotlin-parcelize'
    id "dev.flutter.flutter-gradle-plugin"
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode", "1")
def flutterVersionName = localProperties.getProperty("flutter.versionName", "1.0")

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


android {
    namespace "tommasoscalici.tax_code_flutter_wear_os"
    compileSdk flutter.compileSdkVersion
    ndkVersion "27.0.12077973"

    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "tommasoscalici.taxcode"
        minSdk 30
        targetSdk flutter.targetSdkVersion
        versionCode flutter.versionCode
        versionName flutter.versionName
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lint {
        baseline file("lint-baseline.xml")
    }

    signingConfigs {
        debug {
            keyAlias keystoreProperties['keyAliasDebug']
            keyPassword keystoreProperties['keyPasswordDebug']
            storeFile keystoreProperties['storeFileDebug'] ? file(keystoreProperties['storeFileDebug']) : null
            storePassword keystoreProperties['storePasswordDebug']
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a" // Only ARM architectures for Wear OS
            }
        }
    }
}

dependencies {
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.concurrent:concurrent-futures-ktx:1.2.0"
    implementation "androidx.core:core-ktx:1.16.0"
    implementation "androidx.credentials:credentials:1.5.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.9.1"
    implementation "androidx.wear:wear:1.3.0"
    implementation "androidx.wear:wear-phone-interactions:1.1.0"
    implementation "androidx.wear:wear-remote-interactions:1.1.0"
    implementation "com.google.android.material:material:1.12.0"
    implementation 'com.google.android.gms:play-services-auth:21.3.0'
    implementation 'com.google.android.gms:play-services-wearable:19.0.0'
    implementation 'com.google.android.gms:play-services-base:18.7.2'
    implementation 'com.google.firebase:firebase-analytics:22.5.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.10.2"
}

flutter {
    source = "../.."
}
